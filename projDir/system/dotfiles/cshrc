#########################################
# .cshrc file for HCR system
#########################################

if ( $?prompt ) then
  set prompt = "`hostname`:\!>"
  set autoexpand
  set autolist
  set cdpath = ( ~ )
  set pushdtohome
endif

unset autologout
set autologout=0

umask 002

# --- RUNTIME ---

setenv DATA_HOST archiver
setenv PROJ_DIR ~/projDir
setenv LROSE_INSTALL_DIR ~/lrose
setenv NAVBSP ~/git/HCR_instrument/src/Navigator_xx821/BSP_2.4	# Pentek Navigator BSP dir
setenv DATA_DIR $PROJ_DIR/data
setenv TMP_DIR $DATA_DIR/tmp
setenv LOG_DIR $DATA_DIR/logs
setenv DATA_DISTRIB_LOG_DIR $LOG_DIR/distrib
setenv ERRORS_LOG_DIR $LOG_DIR/status
setenv RESTART_LOG_DIR $LOG_DIR/restart

setenv PROCMAP_HOST localhost
setenv DATA_MAPPER_ACTIVE true
setenv LDATA_FMQ_ACTIVE true

setenv DS_COMM_TIMEOUT_MSECS 60000
setenv DS_PING_TIMEOUT_MSECS 20000

# --- project info --- 

if (-f $PROJ_DIR/system/params/project_info) then
  source $PROJ_DIR/system/params/project_info
endif

# --- Path ---

# Append . to the default path, since some HCR start_* scripts require it in
# the path
set path = ($path .)

 
# Prepend all 'scripts' directories under $PROJ_DIR to the path

if ( -d $PROJ_DIR ) then
  # NOTE: the -L in the find command is needed because $PROJ_DIR is generally
  # a symbolic link
  foreach scriptdir (`find -L $PROJ_DIR -type d -name scripts`)
    set path = ($scriptdir $path)
  end
endif

# Prepend the LROSE binary directory to the path
if ( -d $LROSE_INSTALL_DIR/bin ) then
  set path = ($LROSE_INSTALL_DIR/bin $path)
endif

set savehist=500

# -- core files --
# unlimit coredumpsize

# --- Aliases ---

if ($?prompt) then

  alias sc source ~/.cshrc

  alias setprompt 'set prompt="(`hostname`) `basename $cwd` ! % "'
  alias cd 'cd \!*;setprompt; echo $cwd'
  alias push 'pushd \!*; cd .'
  alias pop 'popd; cd .'

  set prompt = "%m:%~%# "             # tcsh
  set showdots                        # tcsh
  set watch = (1 root any)            # tcsh
  set history=1000
  set nobeep
  set time = 5

  setenv EDITOR emacs
  setenv PAGER less

  alias h history
  alias ssh 'ssh -X'

  alias rm 'rm -i'
  alias mv 'mv -i'
  alias cp 'cp -i' 

  alias df 'df -h'
  set filec
  set fignore = (.o .ln)

  alias catw 'cat `which \!*`'
  alias lessw 'less `which \!*`'
  alias emacsw 'emacs -nw `which \!*`'
  alias viw 'vi `which \!*`'

  alias lst 'ls -l \!* | awk \{s+=\$4\}END\{print\ s\}'
  alias la 'ls -la --color=tty'
  alias ll 'ls -l --color=tty'
  alias lt 'ls -lt --color=tty'
  alias lat 'ls -alt --color=tty'
  alias l 'ls -sF'
  alias L ls -agl
  alias LS ls -aFRts 

  alias gstat 'git status'
  alias gcom 'git commit -am \!*'
  alias gpush 'git push'
  alias gpull 'git pull'
  alias gdiff 'git diff '
  alias gdt 'git difftool -g -y '

  alias xx 'xrdb ~/.Xdefaults'
  alias cvs 'cvs -d :pserver:${CVSUSER}@localhost:/cvs \!*'
  alias cvsforward 'ssh -a -x -C -Y -R 2401:localhost:2401 -R 8888:localhost:8888'
  alias cvsin 'ssh -a -x -C -L 2401:cvs:2401'
  alias cvsout 'ssh -a -x -C -R 2401:cvs:2401'
  alias drx 'ssh -a -XY -R 2401:localhost:2401 -R 8888:localhost:8888 -R 2200:localhost:2200 rds'
  alias rds 'ssh -a -XY -R 2401:localhost:2401 -R 8888:localhost:8888 -R 2200:localhost:2200 rds'

#  alias hsrl-archiver 'ssh -a -XY -R 2401:localhost:2401 eol-lidar@192.168.84.61'
#  alias hsrl 'ssh -a -XY -R 2401:localhost:2401 dhsrl@192.168.84.120'
  alias ahsrl 'ssh -a -XY hsrl@hsrl-archiver'
  alias hsrl-archiver 'ssh -a -XY hsrl@hsrl-archiver'
  alias hsrl 'ssh -a -XY hsrl@hsrl'
  
  alias gitstat 'git status -uno'

  alias gate 'ssh -a -XY -C -L 2401:cvs.rap.ucar.edu:2401 -L 8888:svn.eol.ucar.edu:80 -L 2200:tikal.eol.ucar.edu:22 burghart@gate.ucar.edu'
  alias gated 'ssh -a -XY -C -L 2401:cvs.rap.ucar.edu:2401 -L 8888:svn.eol.ucar.edu:80 dixon@gate.ucar.edu'

  alias acserver 'ssh ads@acserver'

  alias psg 'ps axww | sed -n "1p; \|sed .*/\!*/|d; /\!*/p"'
  alias pslg 'ps alxww | sed -n "1p; \|sed .*/\!*/|d; /\!*/p"'
  alias psug 'ps auxww | sed -n "1p; \|sed .*/\!*/|d; /\!*/p"'
  alias psx 'ps xww | grep -v LogFilter'
  alias ensc 'enscript -2r -fCourier7'

  alias print_procmap 'print_procmap -hb -up -status'
  alias PrintDataMap 'PrintDataMap -all -relt -lreg'
  alias ppm 'print_procmap -hb -up -status'
  alias pdm 'PrintDataMap -all -relt -lreg'
  alias dmd 'DataMapper -delete '
  alias pcheck 'procmap_list_check -proc_list $PROJ_DIR/control/proc_list'
  alias dcheck 'DataMapper_list_check -data_list $PROJ_DIR/control/data_list'
  alias pcheckOK 'pcheck -ok'
  alias dcheckOK 'dcheck -ok'
  alias ph 'get_host_type'
  alias pgen_data 'cd /scr/pgen'
  alias pgen1_data 'cd /scr/pgen1'
  alias pgen2_data 'cd /scr/pgen2'

# save to dvd

  alias savedvd 'growisofs -Z /dev/dvd -r -J'
  alias saveiso2dvd 'growisofs -dvd-compat -Z /dev/dvd='

#    eval `dircolors ~/.dircolors`      # Set up color-ls variables.

  setprompt

endif

